plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.21.3'
    id 'com.github.ben-manes.versions' version '0.23.0'
}

ext {
    appName = "FoggyDicom"
    appLauncher = "FoggyDicom"
    appPackaging = "giraffe.science"
    appModuleName = "giraffe.science.foggydicom"
    appMainClass = "FoggyDicom"
}

mainClassName = "${ext.appModuleName}/${ext.appMainClass}"

repositories {
    mavenCentral()
    maven {
        url = uri("https://www.dcm4che.org/maven2/")
    }
    // For org.weasis.core:weasis-core-img:4.3.0
    maven {
        url = uri("https://github.com/nroduit/mvn-repo/raw/master")
    }
}

configurations {
    runtime.exclude group: "com.sun.media", module: "jai_imageio"
}

javafx {
    version = "14"
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.graphics',
    ]
}

dependencies {
    compile "org.dcm4che.tool:dcm4che-tool-dcm2jpg:5.22.3"
}

jlink {
    imageZip = project.file("${buildDir}/distributions/${appPackaging}-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "${appLauncher}"
    }
}

task dist {
    dependsOn clean, jlinkZip
    description "Calls clean and then jlinkZip [default]"
}

defaultTasks 'dist'